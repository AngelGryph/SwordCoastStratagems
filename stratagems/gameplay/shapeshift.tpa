DEFINE_ACTION_FUNCTION shapeshift STR_VAR version=0 BEGIN  // 0 is the actual component. Call with 1 if you want to build the resources but not give them to PCs

// data

<<<<<<<< .../stratagems-inline/shapeshift_data
filename  name_code  desc_code  animation   icon     enchantment  to_hit  damage speed  dice_num  dice_size strength  str_ex   dexterity   AC    MR    attacks   movement_mod	damage_type
dw#shwe1  13120      13140      32257       ibear    0            0       0      3      1         6         18        76       16          4     10    -1               100		3
dw#shwe2  13121      13141      32257       ibear    1            0       0      3      1         8         19        0        17          0     20    2               100		3
dw#shwe3  13122      13142      32257       ibear    3            0       0      3      2         6         21        0        20          -4    40    2               100		3
dw#shbrp  13123      13143      29185       ibear    2            0       0      3      1         6         18        100      12          7     0     2               200		3
dw#shwol  13124      13144      31488       iwolf    2            0       0      3      1         8         17        0        18          6     0     2               200		1
dw#shwyv  13125      13145      40960       iswospdr 2            0       0      3      2         4         18        76       17          3     0     2               100		1
dw#shsal  13126      13146      59648       iplysala 2            0       0      3      1         8         18        76       19          2     0     1               100		1
dw#shspi  13127      13147      31235       iswospdr 2            0       0      3      1         4         16        0        16          0     0     3               100		1
dw#shfir  13128      13148      29456       ishape07 4            0       0      0      1         10        22        0        25          -5    0     2               100		2
dw#shear  13129      13149      29440       ishape06 4            0       0      0      2         10        25        0        10          -5    0     2               75		2
>>>>>>>>

   <<<<<<<<  .../stratagems-inline/shapeshift_spells.2da
spellname    resource   icon		icon2		override_name	description	remove
dw#shw2      dw#shwe2   -			spin155		1				-1			dw#shwe1
dw#shw3      dw#shwe3   -			spin155		1				-1			dw#shwe2
spcl611      dw#shbrp   -			spin107		1				-1			-
spcl612      dw#shwol   -			spin110		1				-1			-	
spcl632      dw#shspi   -			plyspidr	1				-1			-	
spcl633      dw#shwyv   -			plywyvnb	1				-1			-
spcl634      dw#shsal   -			plybassb	1				-1			-
sppr731      dw#shfir   sppr731b	sppr731c	0				13150		-
sppr732      dw#shear   sppr732b	sppr732c	0				13151		-
>>>>>>>>


   LAF process_table INT_VAR inline=1 STR_VAR table=shapeshift_data function=token_core END
   LAF shapeshift_token_extras END // extra effects
   ACTION_IF version=0 BEGIN
      LAF shapeshift_spells END  // spells
      LAF shapeshift_classfiles END  // classfiles
   END
   LAF check_label STR_VAR label="dw#insectplague" RET value END
   ACTION_IF value BEGIN
       LAF shapeshift_insect_plague END
   END
   LAF shapeshift_token_enemies END // undroppable tokens for enemies
END

//////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////
////  patch used by token-making block
//////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION token_core
                      STR_VAR 
                              filename=""
							  name_code=0
							  desc_code=0
							  animation=0
							  icon=0
							  enchantment=0
                              to_hit=0
                              damage=0
							  speed=0
							  dice_num=1
                              dice_size=0
                              strength=0
                              str_ex=0
                              dexterity=0
                              AC=10
                              MR=0
                              attacks=~-1~
                              movement_mod=100
                              damage_type=3 // slashing is default
BEGIN
	 ACTION_CLEAR_ARRAY core_ability_data
     ACTION_DEFINE_ASSOCIATIVE_ARRAY core_ability_data BEGIN
          ability_type=>1
          ability_icon=>~%icon%~
          ability_icon_loc=>1
          ability_target=>1
          ability_range=>1
          projectile=>1
          overhand=>50
          backhand=>50
          when_drained=>1
          strength_bonus=>1
          to_hit=>~%to_hit%~
          damage_bonus=>~%damage%~
          damage_type=>~%damage_type%~
          speed=>~%speed%~
          ability_numdice=>~%dice_num%~
          ability_dicesize=>~%dice_size%~
     END
     MAKE_PATCH
        icon=>~%icon%~
        say_both_names=> ~%name_code%~
        say_both_descriptions=>~%desc_code%~
        droppable=>1
        magical=>1
        not_copyable=>1
        displayable=>1
        category=>16
        unusable_cleric=>1
        unusable_fighter=>1
        unusable_paladin=>1
        unusable_ranger=>1
        unusable_mage=>1
        unusable_bard=>1
        unusable_thief=>1
        unusable_fighter_mage=>1
        unusable_fighter_cleric=>1
        unusable_fighter_thief=>1
        unusable_fighter_mage_thief=>1
        unusable_cleric_mage=>1
        unusable_cleric_thief=>1
        unusable_mage_thief=>1
        twohanded=>1
        icon_ground=>gward01
        icon_carried=>cmisc6p
        enchantment=>~%enchantment%~
        add_ability => core_ability_data
        add_effect_global_inline'=>~opcode=>0 parameter1=>"%AC%" parameter2=>0x10~
        add_effect_global_inline''=>~opcode=>53 parameter1=>%animation%~ // animation
        add_effect_global_inline'''=>~opcode=>145 parameter2=>0~ // no spells
        add_effect_global_inline''''=>~opcode=>145 parameter2=>1~ // no spells
        add_effect_global_inline'0=>~opcode=>215 target=>1 duration=>1 resource=>SPPOLYMP~ // swirly lights on start
     END
	 ACTION_IF enhanced_edition BEGIN
         OUTER_SPRINT $patch_data(forbid_offhand) 1 	 
	 END 
     ACTION_IF !(movement_mod=100) BEGIN
         OUTER_SPRINT $patch_data(add_effect_global_inline'1) ~opcode=>126 parameter1=>%movement_mod% parameter2=>2~
     END
     ACTION_IF ~dexterity~>0 BEGIN
         OUTER_SPRINT $patch_data(add_effect_global_inline'2) ~opcode=>15 parameter2=>1 parameter1=>%dexterity%~
     END
     ACTION_IF ~strength~>0 BEGIN
         OUTER_SPRINT $patch_data(add_effect_global_inline'3) ~opcode=>44 parameter2=>1 parameter1=>%strength%~
     END
     ACTION_IF str_ex>0 BEGIN
         OUTER_SPRINT $patch_data(add_effect_global_inline'4) ~opcode=>97 parameter2=>1 parameter1=>%str_ex%~
     END
     ACTION_IF ~MR~>0 BEGIN
         OUTER_SPRINT $patch_data(add_effect_global_inline'5) ~opcode=>166  parameter1=>%MR%~
         OUTER_SPRINT $patch_data(add_effect_global_inline'6) ~opcode=>142 parameter2=>63~
     END
     ACTION_IF !(attacks = ~-1~) BEGIN
         OUTER_SPRINT $patch_data(add_effect_global_inline'7) ~opcode=>1 parameter1=>%attacks% parameter2=>1~
     END
     LAF make_item STR_VAR item=~%filename%~ edits=patch_data END
	 // block changes to dexterity, strength 
	MAKE_PATCH
		add_effect_global_inline'0=>"opcode=>101 parameter2=>15 at_end=>1" // actual effects
		add_effect_global_inline'1=>"opcode=>101 parameter2=>44 at_end=>1"
		add_effect_global_inline'2=>"opcode=>101 parameter2=>97 at_end=>1"
		add_effect_global_inline'3=>"opcode=>267 parameter1=>14090 at_end=>1" // 'Strength modification' string et al
		add_effect_global_inline'4=>"opcode=>267 parameter1=>14042 at_end=>1"
		add_effect_global_inline'5=>"opcode=>267 parameter1=>14024 at_end=>1"
		add_effect_global_inline'6=>"opcode=>267 parameter1=>7924 at_end=>1" // enfeebled
		add_effect_global_inline'7=>"opcode=>169 parameter2=>21 at_end=>1" // icons: Strength
		add_effect_global_inline'8=>"opcode=>169 parameter2=>185 at_end=>1" // icons: Cat's Grace
		add_effect_global_inline'9=>"opcode=>169 parameter2=>113 at_end=>1" // icons: enfeeblement
	 END
	 LAF edit_item STR_VAR item="%filename%" edits=patch_data END
END

//////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////
////  Adds additional effects to the tokens
//////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////


DEFINE_ACTION_FUNCTION shapeshift_token_extras BEGIN
      // L2,L3 werewolf: haste
   LAF edit_item STR_VAR item=~dw#shwe2 dw#shwe3~
                      editstring=~add_effect_global_inline=>"opcode=>16"~
   END

   // L3 werewolf: regeneration, 50% resistances to elements
   LAF edit_item STR_VAR item=dw#shwe3
                         editstring=~add_effect_global_inline=>"opcode=>98 parameter1=>1 parameter2=>2"
                                     add_effect_global_inline=>"opcode=>142 parameter2=>87"
                                     add_effect_global_inline=>"opcode=>30 parameter1=>50"
                                     add_effect_global_inline=>"opcode=>84 parameter1=>50"
                                     add_effect_global_inline=>"opcode=>28 parameter1=>50"
                                     add_effect_global_inline=>"opcode=>85 parameter1=>50"
                                     add_effect_global_inline=>"opcode=>27 parameter1=>50"
                                     add_effect_global_inline=>"opcode=>29 parameter1=>50"
                                     add_effect_global_inline=>"opcode=>142 parameter2=>76"~
   END
   
   // wyvern:poisonous sting
   LAF edit_item STR_VAR item=dw#shwyv
                         editstring=~add_effect_inline=>"match=>ability_type=1 opcode=>25 target=>2 parameter1=>3 parameter2=>3 duration=>6 probability1=>25 save_vs_poison=>1"
                                     add_effect_inline=>"match=>ability_type=1 opcode=>142 target=>2 parameter2=>6 duration=>6 probability1=>25 save_vs_poison=>1"
                         ~
   END

   // salamander: fire shield, haste, fire resistance

   LAF edit_item STR_VAR item=dw#shsal
                         editstring=~add_effect_global_inline=>"opcode=>16"
                                     add_effect_global_inline=>"opcode=>30 parameter1=>100 parameter2=>1"
                                     add_effect_global_inline=>"opcode=>215 parameter2=>1 resource=>spfiresr"
                                     add_effect_global_inline=>"opcode=>215 parameter2=>1 resource=>spfirest"
                                     add_effect_global_inline=>"opcode=>232 parameter1=>1 resource=>sppr952d"
                                     add_effect_global_inline=>"opcode=>142 parameter2=>68"
                                     ~
   END

   // sword spider: haste, immunity to web overlay,  protection from web spells, poison
   LAF edit_item STR_VAR item=dw#shspi
                         editstring= ~add_effect_global_inline=>"opcode=>16"
                                      add_effect_global_inline=>"opcode=>101 parameter2=>157"
                                      add_effect_global_inline=>"opcode=>206 resource=>spwi215"
                                      add_effect_global_inline=>"opcode=>206 resource=>spdr201"
                                      add_effect_global_inline=>"opcode=>206 resource=>spin575"
                                      add_effect_global_inline=>"opcode=>206 resource=>spin683"
                                      add_effect_inline=>"match=>ability_type check=>1 opcode=>25 target=>2 parameter1=>2 parameter2=>3 duration=>36 save_vs_poison=>1"
                                      add_effect_inline=>"match=>ability_type check=>1 opcode=>142 target=>2 parameter2=>6  duration=>36 save_vs_poison=>1"
                                      ~
   END
   
   // fire elemental: haste, THAC0=2, fire resistance, fire aura, immunity to weapons, fire damage
   LAF edit_item STR_VAR item=dw#shfir
                         editstring=~add_effect_global_inline=>"opcode=>16 parameter2=>2"
                                     add_effect_global_inline=>"opcode=>120 parameter2=>2"
                                     add_effect_global_inline=>"opcode=>54 parameter1=>2 parameter2=>1"
                                     add_effect_global_inline=>"opcode=>30 parameter1=>100 parameter2=>1"
                                     add_effect_global_inline=>"opcode=>142 parameter2=>164"
                                     add_effect_global_inline=>"opcode=>232 parameter1=>1 resource=>sppr952d"
                                     add_effect_inline=>"match=>ability_type=1 opcode=>12 target=>2 parameter2b=>8 timing=>1 dicenum=>1 dicesize=>10"
                                     ~
   END
   // earth elemental: immunity to normal weapons, THAC0=2, damage resistance, unaffected by other powers that modify the creature's resistances
   OUTER_SET hardiness_strref =RESOLVE_STR_REF (@13152)
   LAF edit_item STR_VAR item=dw#shear
                         editstring=~add_effect_global_inline=>"opcode=>120 parameter2=>2"
                                     add_effect_global_inline=>"opcode=>54 parameter1=>2 parameter2=>1"
                                     add_effect_global_inline=>"opcode=>86 parameter1=>50 parameter2=>1"
                                     add_effect_global_inline=>"opcode=>87 parameter1=>50 parameter2=>1"
                                     add_effect_global_inline=>"opcode=>88 parameter1=>50 parameter2=>1"
                                     add_effect_global_inline=>"opcode=>89 parameter1=>50 parameter2=>1"
                                     add_effect_global_inline=>"opcode=>142 parameter2=>165"
									 add_effect_global_inline=>"opcode=>101 parameter2=>86 at_end=>1"
									 add_effect_global_inline=>"opcode=>101 parameter2=>87 at_end=>1"
									 add_effect_global_inline=>"opcode=>101 parameter2=>88 at_end=>1"
									 add_effect_global_inline=>"opcode=>101 parameter2=>89 at_end=>1"
									 add_effect_global_inline=>"opcode=>206 parameter1=>%hardiness_strref% resource=>%WARRIOR_HARDINESS%"
									 add_effect_global_inline=>"opcode=>206 parameter1=>%hardiness_strref% resource=>SPWISH12"
                                     ~
    END

END

//////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////
////  Undroppable tokens for enemies
//////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION shapeshift_token_enemies BEGIN

   ACTION_DEFINE_ASSOCIATIVE_ARRAY shapeshift_enemy BEGIN
      ear=>nea
      fir=>nfi
      sal=>nsa
      wyv=>nwy
      spi=>nsp
      we1=>nw1
      we2=>nw2
      we3=>nw3
      brp=>nbr
      wol=>nwo
   END

   ACTION_PHP_EACH shapeshift_enemy AS old=>new BEGIN
     LAF clone_item STR_VAR item=~dw#sh%old% => dw#sh%new%~
                            editstring=~twohanded=>0 droppable=>0 delete_opcodes=>145~
     END
   END


END

//////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////
////  The main spell-making block
//////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION shapeshift_spells BEGIN

    OUTER_SPRINT name @13120
    LAF install_spell STR_VAR spell=dw#shw1 location=resource editstring=~set_name=>"%name%"~ END
    LAF process_table INT_VAR inline=1 STR_VAR table=shapeshift_spells.2da function=make_shapeshift_spell END

	// werewolf spells need to remove lower-level tokens
	
	

END


//////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////
////  used to make individual spell
//////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION make_shapeshift_spell
   STR_VAR spellname=""
           resource=""
		   icon="-"
           icon2="-"
		   override_name=1
		   description="-1"
		   remove="-"
BEGIN 
   ACTION_IF override_name BEGIN
	OUTER_SPRINT name_source "%resource%.itm"
   END ELSE BEGIN
    OUTER_SPRINT name_source "%spellname%.spl"
   END
   ACTION_IF FILE_EXISTS_IN_GAME "%name_source%" BEGIN // because HLAs are sometimes not present 
	 COPY_EXISTING "%name_source%" override
		READ_LONG 0x8 name_strref
	 BUT_ONLY
     ACTION_IF FILE_EXISTS_IN_GAME ~%resource%.itm~ BEGIN
	   COPY_EXISTING "dw#shw1.spl" "override/%spellname%.spl"
			WRITE_ASCIIE 0xae "%resource%" (8)
			PATCH_IF "%icon%" STRING_COMPARE_CASE "-" BEGIN 
				WRITE_ASCIIE 0x3a "%icon%" (8)
			END
			PATCH_IF "%icon%" STRING_COMPARE_CASE "-" BEGIN 
				WRITE_ASCIIE 0x76 "%icon2%" (8)
			END
			WRITE_LONG 0x8 name_strref
			PATCH_IF description>=0 BEGIN
				WRITE_LONG 0x50 RESOLVE_STR_REF ((AT %description%))
			END
			PATCH_IF "%remove%" STRING_COMPARE_CASE "-" BEGIN
				LPF ADD_SPELL_EFFECT INT_VAR opcode=112 target=1 timing=1 STR_VAR resource="%remove%" END
			END
	   BUT_ONLY
	  END
   END
END

//////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////
////  used to make classfiles
//////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION shapeshift_classfiles BEGIN

    LAF edit_all_kits STR_VAR parent_class=druid editstring="remove_power=>spcl613" END  // redundant bear power
    COPY_EXISTING_REGEXP "clabdr.*\.2da" override 
	/*
			REPLACE_TEXTUALLY "GR_SPCL611" "AP_SPCL611"// crude swap from Grant to Apply
			REPLACE_TEXTUALLY "GR_SPCL612" "AP_SPCL612"			
			REPLACE_TEXTUALLY "GR_SPCL632" "AP_SPCL632"			
			REPLACE_TEXTUALLY "GR_SPCL633" "AP_SPCL633"			
			REPLACE_TEXTUALLY "GR_SPCL634" "AP_SPCL634"		
	*/		
	MAKE_PATCH
       remove_power=>"spcl643"
       remove_power'=>"spcl644"
       grant_power=>"dw#shw1 1"
       grant_power'=>"dw#shw2 7"
       grant_power''=>"dw#shw3 13"
    END
    LAF edit_kit STR_VAR kit=shapeshifter edits=patch_data END



   COPY_EXISTING_REGEXP ~lu.*\.2da~ ~override~ // we allow for possible extra druid kits
     COUNT_2DA_ROWS 9 ~rowcount~
     FOR (i=0;i<~rowcount~;i=i+1) BEGIN
	  READ_2DA_ENTRY i 1 9 ~abcheck~
	  /*
	  PATCH_IF ~%abcheck%~ STRING_EQUAL_CASE ~GA_SPPR731~ BEGIN
	     SET_2DA_ENTRY i 1 9 ~AP_SPPR731~
	     SET_2DA_ENTRY i 6 9 ~1~
	  END
	  PATCH_IF ~%abcheck%~ STRING_EQUAL_CASE ~GA_SPPR732~ BEGIN
	     SET_2DA_ENTRY i 1 9 ~AP_SPPR732~
	     SET_2DA_ENTRY i 6 9 ~1~
	  END
	  */
     END
   BUT_ONLY
END

//////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////
////  insect plague compatibility
//////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////

DEFINE_ACTION_FUNCTION shapeshift_insect_plague BEGIN


   LAF edit_item STR_VAR item=~dw#shsal dw#shfir~ editstring="immunity_to_spell=>~%CLERIC_SUMMON_INSECTS%A %CLERIC_INSECT_PLAGUE%A %CLERIC_CREEPING_DOOM%A %BLACK_DRAGON_INSECT%A~" END


END

